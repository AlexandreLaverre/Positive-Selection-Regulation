#! /usr/bin/env python

########################################################################################################################
##### PIPELINE TO DETECT POSITIVE SELECTION  #####
# Data requirements:
# input file: list of interested coordinates
# pairwise or multiple genome alignments

# Software requirements: PECAN + Snakemake + Exonerate + TreeTime + seqkit + lsgkm + ucsc-toupper + ucsc-MafsInRegions

# Example of command to execute:
# snakemake -j 8 --config Sp="human" Sample="CEBPA" AlignType="MAF" AncMethod="parsimony" cluster="local" nbPart=6 nbRand=1000 --rerun-incomplete

########################################################################################################################

sp = config["Sp"]
sample = config["Sample"]
nbPart = int(config["nbPart"])
nbRand = int(config["nbRand"])
AncMethod = config["AncMethod"]
cluster = config["cluster"]

prefix = "hsap_" if sp == "human" else "b6-"

if cluster == "cluster":
    path = "/work/FAC/FBM/DEE/mrobinso/evolseq/DetectPosSel"
    split = "split"
else:
    path = "/Users/alaverre/Documents/Detecting_positive_selection"
    split = "gsplit"

workdir: path

pathBED = path + "/results/peaks_calling/" + sp + "/" + sample + "/consensus_peaks.bed"
pathResults = path + "/results/positive_selection/" + sp + "/" + sample
pathAlignment = pathResults + "/Alignments"
pathModel = pathResults + "/Model"
pathScripts = path + "/scripts/detect_positive_selection"

localrules : all, GenerateNegativeSeq, ModelTraining, ModelValidation, ModelPrediction, BED_split,
            InferAncestralPairwise, GetSequencesMultiple, ConcatSeq, TestPosSel

# Define from which type of alignments ancestral sequences should be obtained
if config["AlignType"] == "pairwise":
    ruleorder: InferAncestralPairwise > GetSequencesMultiple
else:
    ruleorder: GetSequencesMultiple > InferAncestralPairwise

########################################################################################################################

rule all:
    input :
        PosSelTest = pathResults + "/PosSelTest_deltaSVM_" + str(nbRand) + "permutations.txt",
        model_validation = pathModel+ "/" + sample + ".cvpred.txt"


rule GenerateNegativeSeq:
    message: "Generate random sequences respecting the focal sequences composition for gkm training"
    input: BED_file = pathBED
    output:
        Positive_seq = pathModel + "/posSet.fa",
        Negative_seq = pathModel + "/negSet.fa"
    log: out = pathResults + "/log/GenerateNegativeSeq.out"
    shell:
        """
        mkdir -p {pathModel}
        Rscript {pathScripts}/GenerateNegativeSeq.R {sp} {sample} {input.BED_file} {cluster} &> {log.out}
        touch {output.Positive_seq} {output.Negative_seq}
        """

rule ModelTraining:
    message: "Training of the gkm-SVM, kmer=10, 4 threads"
    input:
        Positive_seq = pathModel + "/posSet.fa",
        Negative_seq = pathModel + "/negSet.fa"
    output: touch(pathModel + "/" + sample + ".model.txt")
    log: out = pathResults + "/log/ModelTraining.out"
    threads: 4
    priority: 2
    shell:
        """
        gkmtrain -l 10 -T {threads} {input.Positive_seq} {input.Negative_seq} {pathModel}/{sample} &> {log.out}
        """

rule ModelValidation:
    message: "Training of the gkm-SVM, kmer=10, 4 threads, 5-fold cross-validation"
    input:
        Positive_seq = pathModel + "/posSet.fa",
        Negative_seq = pathModel + "/negSet.fa"
    output: touch(pathModel + "/" + sample + ".cvpred.txt")
    log: out = pathResults + "/log/ModelValidation.out"
    threads: 4
    shell:
        """
        gkmtrain -l 10 -x 5 -T {threads} {input.Positive_seq} {input.Negative_seq} {pathModel}/{sample} &> {log.out}
        """

rule ModelPrediction:
    message: "Generate SVM weights for all possible 10-mers"
    input:
        Model = pathModel + "/" + sample + ".model.txt",
        kmer_fasta = path + "/results/positive_selection/kmer.fa"
    output:
        PredictedWeight = pathModel + "/kmer_predicted_weight.txt",
        Prediction_done = pathResults + "/log/ModelPrediction_done"
    log: out = pathResults + "/log/ModelPrediction.out"
    threads: 4
    shell:
        """
        gkmpredict -T {threads} {input.kmer_fasta} {input.Model} {output.PredictedWeight} &> {log.out}
        touch {output.Prediction_done}
        """

rule BED_split:
    message: "Split the list of coordinates for parallelization"
    input: BED_file = pathBED
    output:
        expand(pathResults + "/log/"+ sample +"_part{part}", part=range(100, 100 + nbPart))
    shell:
        """
        mkdir -p {pathResults}/log/
        {split} -d -n l/{nbPart} {input.BED_file} {pathResults}/log/{sample}_part1
        """

rule InferAncestralPairwise:
    message: "Infer ancestral sequences from pairwise alignments"
    input:
        Positive_seq = pathModel + "/posSet.fa",
        BED_file_part= pathResults + "/log/"+ sample +"_part{part}"
    output: touch(pathResults + "/log/" + sample +"_GetAncestral_part{part}_done")
    log: out = pathResults + "/log/" + sample +"_GetAncestral_part{part}.out"
    shell:
        """
        mkdir -p {pathAlignment}
        python {pathScripts}/InferAncestralPairwise.py {sp} {sample} {input.BED_file_part} {AncMethod} {cluster} &> {log.out}
        """

rule GetSequencesMultiple:
    message: "Retrieve focal and ancestral sequences from multiple whole-genome alignment"
    input: BED_file_part= pathResults + "/log/"+ sample +"_part{part}"
    output: touch(pathResults + "/log/" + sample +"_GetAncestral_part{part}_done")
    log: out = pathResults + "/log/" + sample + "_extract_sequences_from_MAF_part{part}.out"
    shell:
        """
        {pathScripts}/extract_sequences_from_MAF.sh {sp} {sample} {input.BED_file_part} {cluster} &> {log.out}
        """

rule ConcatSeq:
    message: "Concatenate and sort all coordinates"
    input: expand(pathResults + "/log/"+ sample +"_GetAncestral_part{part}_done", part=range(100, 100 + nbPart))
    output:
        concat_focal = pathResults + "/sequences/all_focal_sequences.fa",
        concat_focal_filtered = pathResults + "/sequences/filtered_focal_sequences.fa",
        concat_focal_upper = pathResults + "/sequences/filtered_focal_sequences_upper.fa",
        concat_ancestral = pathResults + "/sequences/filtered_ancestral_sequences.fa"
    shell:
        """
        cd {pathAlignment}
        mkdir -p {pathResults}/sequences/
        # Get all non empty ancestral sequences in one file
        find ancestral_sequences -name "*nogap.fa" -size +0 | xargs basename -s _nogap.fa > list_ancestral.txt
        find ancestral_sequences -name "*nogap.fa" -size +0 -exec cat {{}} + > {output.concat_ancestral}

        # Get all corresponding focal sequences in one file
        find focal_sequences -name "*nogap.fa" -size +0 -exec cat {{}} + > {output.concat_focal}
        seqtk subseq {output.concat_focal} {pathAlignment}/list_ancestral.txt > {output.concat_focal_filtered}
        
        # Make sequences in uppercase to remove potential soft repeat mask 
        awk '/^>/ {{print($0)}}; /^[^>]/ {{print(toupper($0))}}' {output.concat_focal_filtered} > {output.concat_focal_upper}
        """

rule TestPosSel:
    message: "Test for positive selection between ancestral and focal sequences"
    input:
        PredictedWeight = pathModel + "/kmer_predicted_weight.txt",
        ancestral_sequences = pathResults + "/sequences/filtered_ancestral_sequences.fa",
        focal_sequences = pathResults + "/sequences/filtered_focal_sequences_upper.fa"
    output: touch(pathResults + "/PosSelTest_deltaSVM_" + str(nbRand) + "permutations.txt")
    threads: nbPart
    shell:
        """
        python {pathScripts}/testPosSelec.py {sp} {sample} {nbRand} submat {cluster} --NbThread {threads}
        """
